apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'com.vmsoft'
version = '1.0.2'

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 32
        versionCode 1
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

//
// MAVEN
//

ext.admin = hasProperty('github_user')

publish.dependsOn('build')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'agpx'
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            pom {
                name = 'Android-GPX-Parser'
                description = 'A GPX Parser for Android.'
                url = 'https://www.vmsoft-bg.com'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/vladimir-paskov/Android-GPX-Parser/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'vmsoft'
                        name = 'Vladimir Paskov, original work by FedericoMatera'
                        email = 'support@vmsoft-bg.com'
                        timezone = '+2'
                    }
                }
                scm {
                    url = 'https://github.com/vladimir-paskov/Android-GPX-Parser'
                    connection = 'scm:git:git://github.com/vladimir-paskov/Android-GPX-Parser.git'
                    developerConnection = 'scm:git:ssh://git@github.com/vladimir-paskov/Android-GPX-Parser.git'
                }
            }
        }
    }

    repositories {
        maven {
            name "GitHubPackages"
            url "https://maven.pkg.github.com/vladimir-paskov/Android-GPX-Parser"
            if (admin) {
                credentials {
                    username "${github_user}"
                    password "${github_token}"
                }
            }
        }
    }
}
